buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.github.jengelman.gradle.plugins:shadow'
  }
}

plugins {
  id 'java'
  id 'idea'
  id 'application'
  id 'jacoco'
  id 'checkstyle'
  id 'findbugs'
//  id 'pmd'
  id "net.ltgt.apt" version "0.10"
  id 'com.github.johnrengelman.shadow' version '5.1.0'
}

mainClassName = 'com.pwittchen.money.transfer.api.Application'
group 'com.pwittchen'
version '1.0-SNAPSHOT'
sourceCompatibility = 1.8

sourceSets {
  main {
    java {
      srcDirs 'src/main/java'
    }
  }

  test {
    java {
      srcDir 'src/test/java'
    }
  }
}

shadowJar {
  baseName = 'app'
  classifier = 'all'
  version = '1.0'
}

jacocoTestReport {
  reports {
    xml.enabled = true
    html.enabled = true
  }

  afterEvaluate {
    classDirectories = files(classDirectories.files.collect {
      fileTree(dir: it,
              excludes: [
                      '**/*_Provide*/**',
                      '**/*_Factory*/**',
                      '**/*_MembersInjector.class',
                      '**/*Dagger*'
              ])
    })
  }
}

checkstyle {
  toolVersion = '6.0'
  configFile = new File(rootDir, 'checkstyle.xml')
}

tasks.withType(FindBugs) {
  reports {
    xml.enabled = false
    html.enabled = true
    reportLevel = "high"
  }
}

test {
  if (System.properties['test.profile'] != 'integration') {
    exclude '**/*integration*'
  } else {
    exclude '**/*com/*'
  }
}

repositories {
  mavenCentral()
}

dependencies {
  implementation 'io.javalin:javalin:3.4.1'
  implementation 'org.slf4j:slf4j-simple:1.7.26'
  implementation 'org.joda:joda-money:1.0.1'
  implementation 'com.google.code.gson:gson:2.8.5'
  implementation 'com.google.dagger:dagger:2.24'
  annotationProcessor 'com.google.dagger:dagger-compiler:2.24'

  testImplementation 'junit:junit:4.12'
  testImplementation 'com.google.truth:truth:1.0'
  testImplementation 'org.mockito:mockito-core:3.0.0'
  testImplementation 'io.rest-assured:rest-assured:4.0.0'
  testImplementation 'net.jodah:concurrentunit:0.4.4'
}
